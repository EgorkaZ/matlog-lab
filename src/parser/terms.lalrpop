use crate::ast;
use ast::{TermNode, TermProvider, TermVar, ExprNode, ExprProvider};
use std::marker::PhantomData;

grammar(term_provider: &TermProvider, expr_provider: &ExprProvider);

pub Proved = {
    "|-" <Expr>,
};

pub Expr = {
    <Impl>,
};

Impl = {
    Disj,
    <l:Disj> "->" <r:Impl> => expr_provider.imp(&l, &r)
};

Disj = {
    Conj,
    <l:Disj> "|" <r:Conj> => expr_provider.disj(&l, &r),
};

Conj = {
    Unary,
    <l:Conj> "&" <r:Unary> => expr_provider.conj(&l, &r),
};

Unary = {
    Predicate,
    "(" <Impl> ")",
    "!" <Unary> => expr_provider.neg(&<>),

    <v:Any> <e:Impl> "$" => expr_provider.any(v, &e),
    <v:Exists> <e:Impl> "$" => expr_provider.ext(v, &e),
};

Any = {
    "@" <Var> ".",
};

Exists = {
    "?" <Var> ".",
};

Predicate: ExprNode = {
    StaticPred => expr_provider.pred(<>),
    DynamicPred => expr_provider.repl(<>),
    <l:Term> "=" <r:Term> => expr_provider.eq(&l, &r),
};

StaticPred: char = {
    r"[A-Z]" => <>.chars().next().unwrap(),
}

DynamicPred: &'input str = {
    r"[a-z]+:Pred" => &<>[..<>.len() - 5],
}


pub Term: TermNode = {
    Summed,
    <l:Term> "+" <r:Summed> => term_provider.add(&l, &r),
};

Summed: TermNode = {
    Multiplier,
    <l:Summed> "*" <r:Multiplier> => term_provider.mul(&l, &r),
};

Multiplier: TermNode = {
    Variable,
    "(" <Term> ")",
    "0" => term_provider.zero(),
    <Multiplier> "'" => term_provider.next(&<>),
};

Variable: TermNode = {
    StaticVar => term_provider.var(<>),
    DynamicVar => term_provider.repl(<>),
};

Var: TermVar = {
    StaticVar => TermVar::Static(<>, PhantomData),
    DynamicVar => TermVar::Dynamic(String::from(<>)),
}

StaticVar: char = {
    r"[a-z]" => <>.chars().next().unwrap(),
}

DynamicVar: &'input str = {
    r"[a-z]+:Var" => &<>[..<>.len() - 4],
}
